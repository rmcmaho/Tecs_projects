// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/1/PC.hdl

/**
 * 16-bit counter with load and reset controls.
 *
 * If reset(t-1) then out(t) = 0
 *    else if load(t-1) then out(t) = in(t-1)
 *         else if inc(t-1) then out(t) = out(t-1) + 1 (integer addition)
 *              else out(t) = out(t-1)
 */

CHIP PC {

    IN  in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    
    // Inc
    Inc16(in=regOut, out=regInc);
    Mux16(a=regOut, b=regInc, sel=inc, out=incOut);

    // Load
    Mux16(a=incOut, b=in, sel=load, out=loadOut);

    // Reset
    Mux16(a=loadOut, b=false, sel=reset, out=resetOut);

    // Set register
    Or(a=load, b=reset, out=tempOr);
    Or(a=inc, b=tempOr, out=orOut);
    Register(in=resetOut, load=orOut, out=out, out=regOut);

}
